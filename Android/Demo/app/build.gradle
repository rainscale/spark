import java.text.SimpleDateFormat

plugins {
    id 'com.android.application'
}

// def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
// Auto-incrementing commit count based on counting commits to master (Build #543)
// def commitCount = Integer.parseInt('git rev-list master --count'.execute([], project.rootDir).text.trim())
// I want to use git tags as my version names (1.2.2)
// def gitCurrentTag = 'git describe --tags --abbrev=0'.execute([], project.rootDir).text.trim()

android {
    namespace 'ale.rains.demo'
    compileSdk 33

    defaultConfig {
        applicationId "ale.rains.demo"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0" + "-" + new SimpleDateFormat("yyyyMMdd").format(new Date())
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }

        buildConfigField "String", "AUTHOR", "\"${author}\""
        flavorDimensions "brand"
        multiDexEnabled = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            keyAlias 'android'
            keyPassword 'android'
            storeFile file("android.jks")
            storePassword 'android'
        }
        release {
            keyAlias 'android'
            keyPassword 'android'
            storeFile file("android.jks")
            storePassword 'android'
        }
    }
    buildTypes {
        debug {
            manifestPlaceholders = [
                    remotePackage: "ale.rains.test",
                    sharedUserId : "ale.rains"
            ]
            minifyEnabled false
            signingConfig signingConfigs.debug
            ndk {
                debugSymbolLevel = 'FULL' // 'SYMBOL_TABLE'
            }
        }
        release {
            manifestPlaceholders = [
                    remotePackage: "ale.rains.test",
                    sharedUserId : "android.uid.system"
            ]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        android.applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    outputFileName = "Demo-" + defaultConfig.versionName + "-" + variant.name + ".apk"
                }
            }
        }
    }
    productFlavors {
        xiaomi {
            dimension "brand"
            buildConfigField 'String', 'REMOTE_PACKAGE', '"com.mi.remote"'
        }
        huawei {
            dimension "brand"
            buildConfigField 'String', 'REMOTE_PACKAGE', '"com.huawei.remote"'
        }
        oppo {
            dimension "brand"
            buildConfigField 'String', 'REMOTE_PACKAGE', '"com.oppo.remote"'
        }
    }
    sourceSets {
        main {
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
    buildFeatures {
        aidl true
        viewBinding true
    }
    ndkVersion '25.1.8937393'
    buildToolsVersion '34.0.0'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'

    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation project(path: ':nanohttpd')
    implementation project(path: ':permission')
    implementation project(path: ':toast')
    implementation project(path: ':util')
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}